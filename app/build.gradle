apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        minSdkVersion 21
        targetSdkVersion 29

        if (System.getenv("AC_APPCIRCLE")) {
            def buildNumber = System.getenv("AC_BUILD_NUMBER")
            versionName = "1.0." + buildNumber
            versionCode = Integer.valueOf(buildNumber)
        } else {
            versionCode 2
            versionName "1.0.1"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "APPCIRCLE_API_URL", "\"${System.env.AC_API_URL}\""    }

    flavorDimensions 'appName'
    productFlavors {
        smartface {
            dimension 'appName'
            applicationId "com.appcircle.StarterApp"
            resValue "string", "app_name", "StarterApp"
            resValue "string", "labelText", "Appcircle Starter"
            manifestPlaceholders = [
                    appIcon: "@drawable/ic_launcher"
            ]
        }
        appcircle {
            dimension 'appName'
            applicationId "com.appcircle.CorporateApp"
            resValue "string", "app_name", "CorporateApp"
            resValue "string", "labelText", "Appcircle Corporate"
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher"
            ]
        }
    }

    buildTypes {
        debug {
            buildConfigField "String", "BUILD_TYPE", "\"debug\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BUILD_TYPE", "\"release\""
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
